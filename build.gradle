plugins {
    id "java"
    id "com.google.cloud.tools.jib" version '3.3.1' apply false
    id "io.freefair.lombok" version "8.0.1" apply false
    id "org.springframework.boot" version "3.0.5" apply false
    id "io.spring.dependency-management" version "1.1.0" apply false
}

group = 'com.github.twobiers'
version = '0.1'

java.sourceCompatibility = JavaVersion.toVersion("17")
java.targetCompatibility = JavaVersion.toVersion("17")

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.cloud.tools.jib'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

configure(subprojects.findAll {it.name != 'core'}) {
    apply plugin: 'com.google.cloud.tools.jib'
    jib {
        from {
            image = "eclipse-temurin:17-jdk-alpine"
        }
        to {
            image = "ghcr.io/twobiers/software-architecture-sustainability-experiment:${rootProject.version}-${project.name}"
        }
        container {
            format = "OCI"
            creationTime = "USE_CURRENT_TIMESTAMP"
        }
    }

    sourceSets {
        main {
            resources {
                srcDirs += [
                        project(':core').sourceSets.main.resources
                ]
            }
        }
    }
}

tasks.create("jibAll") {
    dependsOn subprojects.findAll {it.name != 'core' }.collect { it.tasks.jib }
}

tasks.create("jibDockerBuildAll") {
    dependsOn subprojects.findAll {it.name != 'core' }.collect { it.tasks.jibDockerBuild }
}
